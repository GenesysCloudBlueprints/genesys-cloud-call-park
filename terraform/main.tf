terraform {
  required_providers {
    genesyscloud = {
      source  = "mypurecloud/genesyscloud"
      version = "1.39.0"
    }
  }
}


/*
  Create a Data Action integration
*/
module "data_action" {
  source                          = "git::https://github.com/GenesysCloudDevOps/public-api-data-actions-integration-module?ref=main"
  integration_name                = "Orbit Data Actions OAuth Integration - CX"
  integration_creds_client_id     = var.client_id
  integration_creds_client_secret = var.client_secret
}

/*
   Looks up the id of the user so we can associate it with the queue and group
*/
data "genesyscloud_user" "user" {
  email = var.email
}

/*
  Create Orbit Queue
*/

module "queue_orbit" {
  source                   = "./modules/routing-queue/orbit"
  queue_name               = "Orbit - CX"
  description              = "Orbit Queue generated by CX as Code"
  classifier_queue_members = [data.genesyscloud_user.user.id]
  depends_on               = [data.genesyscloud_user.user]

}

/*
  Create Routing Queue
*/

module "queue_call_park_agent" {
  source                   = "./modules/routing-queue/call-park-agent"
  queue_name               = "Call Park Agent Inbound Queue - CX"
  description              = "Call Park Agent generated by CX as Code"
  classifier_queue_members = [data.genesyscloud_user.user.id]
  depends_on               = [data.genesyscloud_user.user]
}


/*
  Get Waiting Calls
*/
module "get_waiting_calls" {
  source          = "./modules/data-actions/get-waiting-calls-in-specific-queue-based-on-external-tag"
  action_name     = "Get Waiting Calls on Specific Queue Base on External Tag - CX"
  action_category = module.data_action.integration_name
  integration_id  = module.data_action.integration_id
}

/*
  Replace Participant With ANI
*/
module "replace_participant_with_ani" {
  source          = "./modules/data-actions/replace-participant-with-ani"
  action_name     = "Replace Participant with ANI - CX"
  action_category = module.data_action.integration_name
  integration_id  = module.data_action.integration_id
}


/*
  Update External Tag Conversation
*/
module "update_external_tag_conversation" {
  source          = "./modules/data-actions/update-external-tag-on-conversation"
  action_name     = "Update External Tag on Conversation - CX"
  action_category = module.data_action.integration_name
  integration_id  = module.data_action.integration_id
}

/*
  Default In-queue flow
*/

module "default_in_queue_flow" {
  source        = "./modules/flows/default-in-queue-flow"
  flow_name     = "Default In-Queue Flow - CX"
  division_name = var.division_name
}


/*
  In-queue flow orbit parked hold
*/
module "in_queue_flow_orbit_park_hold" {
  source        = "./modules/flows/in-queue-flow-orbit-park-hold"
  flow_name     = "InQueue - Orbit Call Park Hold - CX"
  division_name = var.division_name
}

/*
  Call Park - Agent Inbound Flow
*/
module "call_park_agent_inbound_flow" {
  source             = "./modules/flows/call-park-agent-inbound-flow"
  flow_name          = "Call Park Agent - Inbound Flow - CX"
  division_name      = var.division_name
  queue_name         = module.queue_call_park_agent.queue_name
  in_queue_flow_name = "InQueue - Orbit Call Park Hold - CX"
  queue_id           = module.queue_call_park_agent.queue_id
  depends_on         = [module.in_queue_flow_orbit_park_hold, module.queue_orbit, module.queue_call_park_agent]
}

/*
  Orbit - Parked Call Retrieval
*/
module "orbit_parked_call_retrieval" {
  source               = "./modules/flows/orbit-parked-call-retrieval"
  data_action_category = module.data_action.integration_name
  data_action_name_1   = module.get_waiting_calls.action_name
  data_action_name_2   = module.replace_participant_with_ani.action_name
  data_action_name_3   = module.update_external_tag_conversation.action_name
  queue_id             = module.queue_orbit.queue_id
  flow_name            = "Orbit - Parked Call Retrieval - CX"
  depends_on           = [module.queue_orbit, module.in_queue_flow_orbit_park_hold, module.data_action, module.get_waiting_calls, module.replace_participant_with_ani, module.update_external_tag_conversation]
  division_name        = var.division_name

}

/*
  Add Script
*/
module "script" {
  source           = "./modules/script"
  script_name      = "Orbit Queue Transfer - CX"
  action_name      = "Orbit Data Actions OAuth Integration - CX"
  data_action_name = module.update_external_tag_conversation.action_name
  queue_id         = module.queue_orbit.queue_id
  org_id           = var.org_id
  data_action_id   = format("custom_-_%s", module.update_external_tag_conversation.action_id)
  depends_on       = [module.update_external_tag_conversation, module.queue_orbit, module.data_action]
}








